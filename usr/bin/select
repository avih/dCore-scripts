#!/bb/ash
# (c) Robert Shingledecker 2009
# $1 is title, $2 is file to read from, $3 is offset 0 or 1 (1 default)
#
. /etc/init.d/tc-functions

PATH="/bb:/bin:/sbin:/usr/bin:/usr/sbin"
export PATH
#
[ -z "$2" ] && exit 1
FILEIN="$2"
[ "$FILEIN" == "-" ] && FILEIN=/dev/stdin
OFFSET=1
[ -n "$3" ] && OFFSET="$3"
awk -v title="$1" -v offset="$OFFSET" -v answer="/tmp/select.ans" '
function read_console() {
	"head -1 < /dev/tty" | getline results
	close("head -1 < /dev/tty")
	return results
}

{ A[NR] = $0 }

END {
	if ( NR < 1 ) {
		print "q" > answer
		break
	}
	do {
		system ("clear")
		printf "%s\n\n", title
		for (l=1; l<=20; l++) {
			++j
			if ( j <= NR ) printf "\t%2d. %s\n", j,  A[j]
		}
		printf "\nEnter selection ( 1 - %s ) or (q)uit", NR
		if ( NR > 20 ) printf ", (n)ext/Enter, (p)revious"
		printf ": "
		selection = read_console()
		if (selection == "quit") {
			system ("touch /tmp/select.ans") 
			exit
		}
		if (selection == "q") {
			system ("touch /tmp/select.ans") 
			exit
		}
		if (selection == "p") {
			if ( j > 20 )
				j = j - 40
			else
				j = 0
			continue
		}
		if (selection == "n" || selection == "") {
			if ( j >= NR )
				j = j - 20
			continue
		}
		selection = selection + 0
		if (selection+0 < 1 || selection+0 >= 1 ) j = j - 20
	} while (selection < 1 || selection > NR)
	if ( offset == 1 && selection != "q")
		print A[selection] > answer
	else
		print selection > answer
} ' < "$FILEIN"
